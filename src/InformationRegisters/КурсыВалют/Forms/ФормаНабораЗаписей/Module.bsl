#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьНадписьКурсыАктуальны();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновленыКурсыВалют" Тогда
		ОбновитьНадписьКурсыАктуальны();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НадписьКурсыВалютНажатие(Элемент)
	
	ООЗакрытиеФормыВводаКурсов = Новый ОписаниеОповещения("ЗакрытиеФормыВводаКурсов", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ФормаВводаКурсовВалют", , ЭтотОбъект, , , ,
				ООЗакрытиеФормыВводаКурсов , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ОбновитьНадписьКурсыАктуальны();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыКурсы

&НаКлиенте
Процедура КурсыПриИзменении(Элемент)
	
	ОбновитьНадписьКурсыАктуальны();	
	
КонецПроцедуры

&НаКлиенте
Процедура КурсыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьНадписьКурсыАктуальны();
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьНадписьКурсыАктуальны() Экспорт

	ЗаголовокКурсыВалют = Элементы.НадписьКурсыВалют;

	Если КурсыВалютАктуальны(ТекущаяДата()) Тогда
		
		ЗаголовокКурсыВалют.Заголовок = "Курсы валют актуальны";
		ЗаголовокКурсыВалют.ЦветТекста = WebЦвета.Зеленый;
		
	Иначе
		
		ЗаголовокКурсыВалют.Заголовок = "Курсы валют не актуальны! Исправить!";
		ЗаголовокКурсыВалют.ЦветТекста = WebЦвета.Красный;
		
	КонецЕсли;

КонецПроцедуры	

&НаКлиенте
Процедура ЗакрытиеФормыВводаКурсов(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Курсы.Обновить();
	
КонецПроцедуры	

&НаСервереБезКонтекста
Функция ПолучитьТаблицуАктуальностиКурсов(ДатаКурсов, флгТолькоНеактуальныеКурсы = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&ДатаКурсов, Валюта В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Остатки.Валюта
	|				ИЗ
	|					РегистрНакопления.ОстаткиПоСчетам.Остатки(&ДатаКурсов, Валюта.Используется И Валюта <> &ВалютаУчета) КАК Остатки)
	|			) КАК КурсыВалютСрезПоследних"
	+ ?(флгТолькоНеактуальныеКурсы, " Где КурсыВалютСрезПоследних.Период < НачалоПериода(&ДатаКурсов, День)", "");
	Запрос.УстановитьПараметр("ДатаКурсов", КонецДня(ДатаКурсов));
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ОсновнаяВалютаУчета.Получить());
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции	                                             

&НаСервереБезКонтекста
Функция КурсыВалютАктуальны(ДатаКурсов) Экспорт 
	
	ТаблицаКурсов = ПолучитьТаблицуАктуальностиКурсов(ДатаКурсов, Истина);
	Возврат ТаблицаКурсов.Количество() = 0;
	
КонецФункции	

#КонецОбласти
